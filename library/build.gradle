apply plugin: 'com.android.library'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-android'
apply plugin: 'bintray-release'

ext {
    // version* variables must not be longer than two digits
    versionMajor = 4
    versionMinor = 1
    versionPatch = 3
    versionAlpha = 0
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"
    resourcePrefix 'ym_'

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 25
        versionCode getAppVersionCode()
        versionName getAppVersionName()
        consumerProguardFiles 'proguard-cfg.txt'
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        buildConfigField 'boolean', 'MOCK_ENABLED', 'false'
    }

    flavorDimensions "common"
    productFlavors {
        dev {
            dimension "common"
        }
        mSDKMock {
            dimension "common"
            buildConfigField 'boolean', 'MOCK_ENABLED', 'true'
        }
    }

    lintOptions {
        abortOnError false
    }

    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/LICENSE.txt'
    }
}

def getAppVersionName() {
    return "${versionMajor}.${versionMinor}.${versionPatch}"
}

int getAppVersionCode() {
    return versionMajor * 1000000 + versionMinor * 10000 + versionPatch * 100 + versionAlpha
}

dependencies {
    compile 'com.yandex.money.api:yandex-money-sdk-java:7.2.21'
    compile 'com.android.support:support-annotations:25.3.1'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
    compile "com.squareup.okhttp3:mockwebserver:4.0.1"
    compile 'com.google.code.gson:gson:2.8.5'
    androidTestCompile 'com.jayway.android.robotium:robotium-solo:5.5.4'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}

publish {
    userOrg = 'yandex-money'
    groupId = 'com.yandex.money.api'
    artifactId = 'yandex-money-sdk-android'
    publishVersion = getAppVersionName()
    website = 'https://github.com/yandex-money/yandex-money-sdk-android'
    issueTracker = "${website}/issues"
    desc = "Yandex.Money SDK for Android"
    repository = "${website}.git"
}
repositories {
    mavenCentral()
}
